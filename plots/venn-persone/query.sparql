SELECT ?occupationLabel (COUNT ( ?occupationLabel) AS ?count)
WITH {
SELECT ?item ?gender ?occupation ?citizenship ?birth WHERE {  
  ?item wdt:P31 wd:Q5; #P31 means we are looking for items that are instances of the entity
        wdt:P21 ?gender;
        wdt:P106 ?occupation;
        wdt:P27 ?citizenship;
        wdt:P569 ?birth.  hint:Prior hint:rangeSafe true.
    filter (?birth > "1820-00-00"^^xsd:dateTime && ?birth < "1821-00-00"^^xsd:dateTime)
}
} AS %results
WHERE {
INCLUDE %results.
?item rdfs:label ?itemLabel. FILTER( LANG(?itemLabel)="it" )
?gender rdfs:label ?genderLabel. FILTER( LANG(?genderLabel)="en" )
?occupation rdfs:label ?occupationLabel. FILTER( LANG(?occupationLabel)="it" )
?citizenship rdfs:label ?citizenshipLabel. FILTER( LANG(?citizenshipLabel)="en" )
} GROUP BY ?occupationLabel
ORDER BY DESC(?count) ?occupationLabel

